buildscript {
    dependencies {
        classpath 'com.h2database:h2:1.4.191'
    }
}

plugins {
    id 'org.springframework.boot' version '1.5.3.RELEASE'
    id 'groovy'
    id 'idea'
    id "org.flywaydb.flyway" version "4.2.0"
}

def defaultEncoding = 'UTF-8'
def jdkVersion = '1.8'

task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
}

ext['thymeleaf.version'] = '3.0.2.RELEASE'
ext['thymeleaf-layout-dialect.version'] = '2.1.1'

jar {
    baseName = 'otameshiwebbapp'
    version =  '0.0.1-SNAPSHOT'
}

compileJava {
    options.encoding = defaultEncoding
}

compileTestJava {
    options.encoding = defaultEncoding
}

sourceSets {
    integrationTest {
        groovy {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/groovy')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile("org.springframework.boot:spring-boot-devtools")
    compile("org.springframework.boot:spring-boot-starter-actuator")
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-thymeleaf")
    compile('com.h2database:h2:1.4.195')
    testCompile("org.springframework.boot:spring-boot-starter-test")
    testCompile 'org.codehaus.groovy:groovy-all:2.4.11'
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    testCompile 'org.spockframework:spock-spring:1.0-groovy-2.4'
    testCompile 'org.jsoup:jsoup:1.10.2'
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
}
check.dependsOn integrationTest

flyway {
    url = 'jdbc:h2:file:~/dev/data/study_springboot'
    user = 'sa'
}

idea {
    project {
        jdkName = jdkVersion
        languageLevel = jdkVersion
    }
    module {
        inheritOutputDirs = false
        scopes.TEST.plus += [ configurations.integrationTestCompile]
    }
}
